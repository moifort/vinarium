resource "google_service_account" "function" {
  project      = google_project.this.project_id
  account_id   = "cave-a-vin-runtime"
  display_name = "Cave-a-Vin runtime"
}

resource "google_project_iam_member" "function_firestore" {
  project = google_project.this.project_id
  role    = "roles/datastore.user"
  member  = "serviceAccount:${google_service_account.function.email}"
}

resource "google_secret_manager_secret_iam_member" "function" {
  for_each  = google_secret_manager_secret.this
  project   = google_project.this.project_id
  secret_id = each.value.secret_id
  role      = "roles/secretmanager.secretAccessor"
  member    = "serviceAccount:${google_service_account.function.email}"
}

resource "google_storage_bucket" "source" {
  project                     = google_project.this.project_id
  name                        = "${google_project.this.project_id}-fn-source"
  location                    = var.firestore_location
  force_destroy               = true
  uniform_bucket_level_access = true

  depends_on = [google_project_service.apis]
}

data "archive_file" "source" {
  type        = "zip"
  source_dir  = "${path.root}/../.output/server"
  output_path = "${path.root}/../.output/server-source.zip"
}

resource "google_storage_bucket_object" "source" {
  name   = "source-${data.archive_file.source.output_md5}.zip"
  bucket = google_storage_bucket.source.name
  source = data.archive_file.source.output_path
}

resource "google_cloudfunctions2_function" "server" {
  provider = google-beta
  project  = google_project.this.project_id
  name     = "cave-a-vin-server"
  location = var.region

  build_config {
    runtime     = "nodejs22"
    entry_point = "server"

    source {
      storage_source {
        bucket = google_storage_bucket.source.name
        object = google_storage_bucket_object.source.name
      }
    }
  }

  service_config {
    available_memory      = "512M"
    timeout_seconds       = 60
    max_instance_count    = 100
    min_instance_count    = 0
    service_account_email = google_service_account.function.email

    environment_variables = {
      NITRO_FIREBASE_PROJECT_ID = google_project.this.project_id
    }

    dynamic "secret_environment_variables" {
      for_each = google_secret_manager_secret.this
      content {
        key        = "NITRO_${replace(upper(secret_environment_variables.key), "-", "_")}"
        project_id = google_project.this.project_id
        secret     = secret_environment_variables.value.secret_id
        version    = "latest"
      }
    }
  }

  depends_on = [
    google_project_iam_member.function_firestore,
    google_secret_manager_secret_iam_member.function,
  ]
}

# Auth is enforced inside the function itself (server/middleware/auth.ts
# verifies the Firebase ID token), so the underlying Cloud Run service is
# left publicly invokable.
resource "google_cloud_run_service_iam_member" "invoker" {
  project  = google_project.this.project_id
  location = var.region
  service  = google_cloudfunctions2_function.server.name
  role     = "roles/run.invoker"
  member   = "allUsers"
}
